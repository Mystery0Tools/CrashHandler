/*
 * Created by Mystery0 on 18-3-23 下午4:46.
 * Copyright (c) 2018. All Rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
	compileSdkVersion 30

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion 30
		versionCode 1
		versionName POM_VERSION
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
	mavenCentral()
}

task generateSourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier 'sources'
}

afterEvaluate {
	publishing {
		publications {
			Production(MavenPublication) {
				from components.release
				groupId = POM_GROUP_ID
				artifactId = POM_ARTIFACT_ID
				version = POM_VERSION
				// 上传source，这样使用方可以看到方法注释
				artifact generateSourcesJar
			}
		}
		repositories {
			// 定义一个 maven 仓库
			maven {
				// 根据 versionName 来判断仓库地址
				url = POM_VERSION.endsWith('SNAPSHOT') ? SNAPSHOT_REPOSITORY_URL : RELEASE_REPOSITORY_URL
				// 仓库用户名密码
				credentials {
					username = System.getenv("NEXUS_USER")
					password = System.getenv("NEXUS_KEY")
				}
			}
		}
	}
}